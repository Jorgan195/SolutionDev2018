OBJECT Codeunit 123456731 Seminar Jnl.-Check Line
{
  OBJECT-PROPERTIES
  {
    Date=01/08/13;
    Time=23:38:38;
    Modified=Yes;
    Version List=CSD1.00;
  }
  PROPERTIES
  {
    TableNo=123456731;
    OnRun=BEGIN
            RunCheck(Rec);
          END;

  }
  CODE
  {
    VAR
      GLSetup@1000 : Record 98;
      UserSetup@1001 : Record 91;
      DimMgt@1006 : Codeunit 408;
      AllowPostingFrom@1002 : Date;
      AllowPostingTo@1003 : Date;
      Text000@1004 : TextConst 'ENU=cannot be a closing date.';
      Text001@1005 : TextConst 'ENU=is not within your range of allowed posting dates.';
      Text002@1007 : TextConst 'ENU=The combination of dimensions used in %1 %2, %3, %4 is blocked. %5';
      Text003@1008 : TextConst 'ENU=A dimension used in %1 %2, %3, %4 has caused an error. %5';

    PROCEDURE RunCheck@3(VAR SemJnlLine@1000 : Record 123456731);
    VAR
      TableID@1001 : ARRAY [10] OF Integer;
      No@1002 : ARRAY [10] OF Code[20];
    BEGIN
      WITH SemJnlLine DO BEGIN
        IF EmptyLine THEN
          EXIT;

        TESTFIELD("Posting Date");
        TESTFIELD("Instructor Resource No.");
        TESTFIELD("Seminar No.");

        CASE "Charge Type" OF
          "Charge Type"::Instructor:
            TESTFIELD("Instructor Resource No.");
          "Charge Type"::Room:
            TESTFIELD("Room Resource No.");
          "Charge Type"::Participant:
            TESTFIELD("Participant Contact No.");
        END;

        IF Chargeable THEN
          TESTFIELD("Bill-to Customer No.");

        IF "Posting Date" = CLOSINGDATE("Posting Date") THEN
          FIELDERROR("Posting Date",Text000);

        IF (AllowPostingFrom = 0D) AND (AllowPostingTo = 0D) THEN BEGIN
          IF USERID <> '' THEN
            IF UserSetup.GET(USERID) THEN BEGIN
              AllowPostingFrom := UserSetup."Allow Posting From";
              AllowPostingTo := UserSetup."Allow Posting To";
            END;
          IF (AllowPostingFrom = 0D) AND (AllowPostingTo = 0D) THEN BEGIN
            GLSetup.GET;
            AllowPostingFrom := GLSetup."Allow Posting From";
            AllowPostingTo := GLSetup."Allow Posting To";
          END;
          IF AllowPostingTo = 0D THEN
            AllowPostingTo := 31129999D;
        END;
        IF ("Posting Date" < AllowPostingFrom) OR ("Posting Date" > AllowPostingTo) THEN
          FIELDERROR("Posting Date",Text001);

        IF ("Document Date" <> 0D) THEN
          IF ("Document Date" = CLOSINGDATE("Document Date")) THEN
            FIELDERROR("Document Date",Text000);

        IF NOT DimMgt.CheckDimIDComb("Dimension Set ID") THEN
          ERROR(
            Text002,
            TABLECAPTION,"Journal Template Name",
            "Journal Batch Name","Line No.",
            DimMgt.GetDimCombErr);
        TableID[1] := DATABASE::Seminar;
        No[1] := "Seminar No.";
        TableID[2] := DATABASE::Resource;
        No[2] := "Instructor Resource No.";
        TableID[3] := DATABASE::Resource;
        No[3] := "Room Resource No.";
        IF NOT DimMgt.CheckDimValuePosting(TableID,No,"Dimension Set ID") THEN
          IF "Line No." <> 0 THEN
            ERROR(
              Text003,
              TABLECAPTION,"Journal Template Name",
              "Journal Batch Name","Line No.",
              DimMgt.GetDimValuePostingErr)
          ELSE
            ERROR(DimMgt.GetDimValuePostingErr);
      END;
    END;

    BEGIN
    {
      CSD1.00 - 2013-04-02 - D. E. Veloper
        Chapter 4 - Lab 2
          - Created new codeunit

      CSD1.00 - 2013-08-01 - D. E. Veloper
        Chapter 8 - Lab 1
          - Integrated Dimensions functionality
    }
    END.
  }
}

