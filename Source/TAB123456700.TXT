OBJECT Table 123456700 Seminar
{
  OBJECT-PROPERTIES
  {
    Date=01/08/13;
    Time=18:36:14;
    Modified=Yes;
    Version List=CSD1.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 SeminarSetup.GET;
                 SeminarSetup.TESTFIELD("Seminar Nos.");
                 NoSeriesMgt.InitSeries(SeminarSetup."Seminar Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;

               DimMgt.UpdateDefaultDim(
                 DATABASE::Seminar,"No.",
                 "Global Dimension 1 Code","Global Dimension 2 Code");
             END;

    OnModify=BEGIN
               "Last Date Modified" := TODAY;
             END;

    OnDelete=BEGIN
               CommentLine.RESET;
               CommentLine.SETRANGE("Table Name",CommentLine."Table Name"::Seminar);
               CommentLine.SETRANGE("No.","No.");
               CommentLine.DELETEALL;

               DimMgt.DeleteDefaultDim(DATABASE::Seminar,"No.");
             END;

    OnRename=BEGIN
               "Last Date Modified" := TODAY;
             END;

    CaptionML=ENU=÷;
    LookupPageID=Page123456700;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  SeminarSetup.GET;
                                                                  NoSeriesMgt.TestManual(SeminarSetup."Seminar Nos.");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=ENU=No. }
    { 2   ;   ;Name                ;Text50        ;OnValidate=BEGIN
                                                                IF ("Search Name" = UPPERCASE(xRec.Name)) OR ("Search Name" = '') THEN BEGIN
                                                                  "Search Name" := Name;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Name }
    { 3   ;   ;Seminar Duration    ;Decimal       ;CaptionML=ENU=Seminar Duration;
                                                   DecimalPlaces=0:1 }
    { 4   ;   ;Minimum Participants;Integer       ;CaptionML=ENU=Minimum Participants }
    { 5   ;   ;Maximum Participants;Integer       ;CaptionML=ENU=Maximum Participants }
    { 6   ;   ;Search Name         ;Code50        ;CaptionML=ENU=Search Name }
    { 7   ;   ;Blocked             ;Boolean       ;CaptionML=ENU=Blocked }
    { 8   ;   ;Last Date Modified  ;Date          ;CaptionML=ENU=Last Date Modified;
                                                   Editable=No }
    { 9   ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Comment Line" WHERE (Table Name=CONST(Seminar),
                                                                                           No.=FIELD(No.)));
                                                   CaptionML=ENU=Comment;
                                                   Editable=No }
    { 10  ;   ;Seminar Price       ;Decimal       ;CaptionML=ENU=Seminar Price;
                                                   AutoFormatType=1 }
    { 11  ;   ;Gen. Prod. Posting Group;Code10    ;TableRelation="Gen. Product Posting Group";
                                                   OnValidate=BEGIN
                                                                IF xRec."Gen. Prod. Posting Group" <> "Gen. Prod. Posting Group" THEN BEGIN
                                                                  IF GenProdPostingGroup.ValidateVatProdPostingGroup(GenProdPostingGroup,"Gen. Prod. Posting Group") THEN BEGIN
                                                                    VALIDATE("VAT Prod. Posting Group",GenProdPostingGroup."Def. VAT Prod. Posting Group");
                                                                  END;
                                                                END;
                                                              END;

                                                   CaptionML=ENU=Gen. Prod. Posting Group }
    { 12  ;   ;VAT Prod. Posting Group;Code10     ;TableRelation="VAT Product Posting Group";
                                                   CaptionML=ENU=VAT Prod. Posting Group }
    { 13  ;   ;No. Series          ;Code10        ;CaptionML=ENU=No. Series;
                                                   Editable=No }
    { 21  ;   ;Global Dimension 1 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Global Dimension 1 Code");
                                                              END;

                                                   CaptionML=ENU=Global Dimension 1 Code;
                                                   CaptionClass='1,1,1' }
    { 22  ;   ;Global Dimension 2 Code;Code20     ;TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Global Dimension 2 Code");
                                                              END;

                                                   CaptionML=ENU=Global Dimension 2 Code;
                                                   CaptionClass='1,1,2' }
    { 30  ;   ;Date Filter         ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Date Filter }
    { 31  ;   ;Charge Type Filter  ;Option        ;FieldClass=FlowFilter;
                                                   CaptionML=ENU=Charge Type Filter;
                                                   OptionCaptionML=ENU=Instructor,Room,Participant,Charge;
                                                   OptionString=Instructor,Room,Participant,Charge }
    { 32  ;   ;Total Price         ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Seminar Ledger Entry"."Total Price" WHERE (Seminar No.=FIELD(No.),
                                                                                                               Posting Date=FIELD(Date Filter),
                                                                                                               Charge Type=FIELD(Charge Type Filter)));
                                                   CaptionML=ENU=Total Price }
    { 33  ;   ;Total Price (Not Chargeable);Decimal;
                                                   FieldClass=FlowField;
                                                   CalcFormula=Sum("Seminar Ledger Entry"."Total Price" WHERE (Seminar No.=FIELD(No.),
                                                                                                               Posting Date=FIELD(Date Filter),
                                                                                                               Charge Type=FIELD(Charge Type Filter),
                                                                                                               Chargeable=CONST(No)));
                                                   CaptionML=ENU=Total Price (Not Chargeable) }
    { 34  ;   ;Total Price (Chargeable);Decimal   ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Seminar Ledger Entry"."Total Price" WHERE (Seminar No.=FIELD(No.),
                                                                                                               Posting Date=FIELD(Date Filter),
                                                                                                               Charge Type=FIELD(Charge Type Filter),
                                                                                                               Chargeable=CONST(Yes)));
                                                   CaptionML=ENU=Total Price (Chargeable) }
    { 41  ;   ;Global Dimension 1 Filter;Code20   ;FieldClass=FlowFilter;
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1));
                                                   CaptionML=ENU=Global Dimension 1 Filter;
                                                   CaptionClass='1,3,1' }
    { 42  ;   ;Global Dimension 2 Filter;Code20   ;FieldClass=FlowFilter;
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2));
                                                   CaptionML=ENU=Global Dimension 2 Filter;
                                                   CaptionClass='1,3,2' }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Search Name                              }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      SeminarSetup@1000 : Record 123456701;
      CommentLine@1001 : Record 97;
      Seminar@1002 : Record 123456700;
      GenProdPostingGroup@1003 : Record 251;
      NoSeriesMgt@1004 : Codeunit 396;
      DimMgt@1005 : Codeunit 408;

    PROCEDURE AssistEdit@2() : Boolean;
    BEGIN
      WITH Seminar DO BEGIN
        Seminar := Rec;
        SeminarSetup.GET;
        SeminarSetup.TESTFIELD("Seminar Nos.");
        IF NoSeriesMgt.SelectSeries(SeminarSetup."Seminar Nos.",xRec."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := Seminar;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE ValidateShortcutDimCode@1(FieldNumber@1000 : Integer;VAR ShortcutDimCode@1001 : Code[20]);
    BEGIN
      DimMgt.ValidateDimValueCode(FieldNumber,ShortcutDimCode);
      DimMgt.SaveDefaultDim(DATABASE::Seminar,"No.",FieldNumber,ShortcutDimCode);
      MODIFY;
    END;

    BEGIN
    {
      CSD1.00 - 2013-02-02 - D. E. Veloper
        Chapter 2 - Lab 2
          - Created new table

      CSD1.00 - 2013-07-01 - D. E. Veloper
        Chapter 7 - Lab 1
          - Added fields:
            - Date Filter
            - Charge Type Filter
            - Total Price
            - Total Price (Not Chargeable)
            - Total Price (Chargeable)

      CSD1.00 - 2013-08-01 - D. E. Veloper
        Chapter 8 - Lab 1
          - Added fields:
            - Global Dimension 1 Code
            - Global Dimension 2 Code
            - Global Dimension 1 Filter
            - Global Dimension 2 Filter
          - Added functions:
            - ValidateShortcutDimCode
          - Updated OnInsert and OnModify triggers
    }
    END.
  }
}

